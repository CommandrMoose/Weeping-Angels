buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        maven { url = "https://plugins.gradle.org/m2/" }
        maven { url = 'https://sizableshrimp.me/maven' }
        jcenter()
        mavenCentral()

    }
    dependencies {
        classpath group: 'me.sizableshrimp.parchmenttest', name: 'ForgeGradle', version: '4.1.+', changing: true
        classpath "gradle.plugin.com.matthewprenger:CurseGradle:1.4.0"
    }
}
apply plugin: 'net.minecraftforge.gradle'
// Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: "com.matthewprenger.cursegradle"


version = "${mod_version}"
group = 'me.suff.mc.angels' // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "${project.jar_name}-1.16.x"

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.

// Process resources on build
processResources {
    // This will ensure that this task is redone when the versions change.
    inputs.property 'version', project.version

    // Replace stuff in mods.toml, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'META-INF/mods.toml'

        // Replace version
        expand 'version': project.version
    }

    // Copy everything else except the mods.toml
    from(sourceSets.main.resources.srcDirs) {
        exclude 'META-INF/mods.toml'
    }
}

println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))
minecraft {
    //mappings channel: "${mappings_type}", version: "${mappings_version}"
    mappings channel: 'parchment', version: '20210608-SNAPSHOT-1.16.5'

    runs {
        client {
            workingDirectory project.file('run')

            if (project.hasProperty('mc_uuid')) {
                args '--uuid', project.getProperty('mc_uuid')
            }
            if (project.hasProperty('mc_username')) {
                args '--username', project.getProperty('mc_username')
            }
            if (project.hasProperty('mc_accessToken')) {
                args '--accessToken', project.getProperty('mc_accessToken')
            }

            mods {
                weeping_angels {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')
            mods {
                weeping_angels {
                    source sourceSets.main
                }
            }
        }


        data {
            workingDirectory project.file('run')
            args '--mod', 'weeping_angels', '--all', '--output', file('src/generated/resources/')

            mods {
                weeping_angels {
                    source sourceSets.main
                }
            }
        }
    }
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

}

curseforge {
    if (project.hasProperty('curseforge') && project.hasProperty('weeping_angels')) {
        apiKey = rootProject.curseforge
        project {
            id = rootProject.weeping_angels
            changelog = new File("${project.projectDir}/changelog.html").text
            changelogType = 'html'
            releaseType = 'release'
            addGameVersion '1.16.3'
            addGameVersion '1.16.4'
            addGameVersion '1.16.5'
            mainArtifact(jar) {
                displayName = "[1.16.X] Weeping Angels - $project.version"
            }
        }
    }
}

repositories {
    maven { url 'https://www.dogforce-games.com/maven/' }
    maven { url "https://www.cursemaven.com" }
    maven { url 'https://jitpack.io' }
    flatDir { dirs 'libMods' }
}


dependencies {
    minecraft "net.minecraftforge:forge:${forge_version}"
    implementation fg.deobf("curse.maven:configured-457570:3244520")
    implementation fg.deobf("curse.maven:catalogue-459701:3285353")
    implementation fg.deobf('undefined:time:1.0')
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : "weeping_angels",
                "Specification-Vendor"    : "suff99",
                "Specification-Version"   : "${version}",
                "Implementation-Title"    : "Weeping Angels",
                "Implementation-Version"  : "${version}",
                "Implementation-Vendor"   : "suff99",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}


jar.finalizedBy('reobfJar')

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file:///${project.projectDir}/mcmodsrepo"
        }
    }
}